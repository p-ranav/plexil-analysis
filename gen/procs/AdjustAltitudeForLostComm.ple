Boolean Lookup ExpectingAltitudeClearance;
Integer Lookup ClearanceAltitude;
Integer Lookup ExpectedAltitude;
Integer Lookup MEA;  // a constant for now
Command setLostCommAltitude (Integer altitude);
Command pprint (...);

AdjustAltitudeForLostComm:
{
  Integer altitude;

  // Adjust altitude to the HIGHEST of the following for the current
  // route segment.

  // 1. The altitude assigned in the last clearance.

  altitude = Lookup (ClearanceAltitude);
  pprint ("AdjustForLostComm: considering clearance altitude:", altitude);
    
  // 2. The minimum en route altitude for the current route segment.

  if (altitude < Lookup(MEA)) {
    altitude = Lookup(MEA);
    pprint ("AdjustForLostComm: considering MEA:", altitude);
  }
  endif

  // 3. The altitude advised in an expected further clearance (when the
  //    condition for this EFC is true)

  if (Lookup (ExpectingAltitudeClearance) &&
      altitude < Lookup (ExpectedAltitude)) {
    altitude = Lookup (ExpectedAltitude);
    pprint ("AdjustForLostComm: selecting EFC altitude:", altitude);
  }
  endif

  setLostCommAltitude (altitude);
}
